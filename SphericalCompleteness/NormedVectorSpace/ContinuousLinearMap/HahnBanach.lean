import SphericalCompleteness.NormedVectorSpace.Orthogonal.OrthComp

open ContinuousLinearMap

namespace SphericallyCompleteSpace

/--
Hahnâ€“Banach extension theorem in the ultrametric setting, assuming spherical completeness.

Given a nontrivially normed field `ğ•œ`, normed `ğ•œ`-spaces `E` and `F` equipped with an
ultrametric distance, a submodule `D : Submodule ğ•œ E` that is spherically complete
(`SphericallyCompleteSpace D`), and a continuous linear map `f : D â†’L[ğ•œ] F`,
this theorem produces an extension `f' : E â†’L[ğ•œ] F` such that:

* `f'` agrees with `f` on `D` (via the subtype coercion `âŸ¨v, hvâŸ©`), and
* the operator norm is preserved: `â€–f'â€– = â€–fâ€–`.

This is a norm-preserving extension result (isometric on operator norm) for continuous
linear maps from a spherically complete subspace in a non-Archimedean (ultrametric) context.
-/
theorem hahn_banach {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
{E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [IsUltrametricDist E]
(D : Submodule ğ•œ E)
{F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [IsUltrametricDist F]
[hd : SphericallyCompleteSpace D] (f : D â†’L[ğ•œ] F) :
âˆƒ f' : E â†’L[ğ•œ] F,
  (âˆ€ v : E, (hv : v âˆˆ D) â†’ f' v = f âŸ¨v, hvâŸ©) âˆ§ â€–f'â€– = â€–fâ€– := by
  use comp f (OrthProj ğ•œ D)
  constructor
  Â· intro v hv
    rw [comp_apply, (SetLike.coe_eq_coe.mp <| OrthProj_id ğ•œ D v hv : ((OrthProj ğ•œ D) v) = âŸ¨v,hvâŸ©)]
  Â· refine eq_of_le_of_ge ((opNorm_le_iff <| opNorm_nonneg f).mpr fun x => ?_) ?_
    Â· rw [comp_apply]
      refine le_trans (le_opNorm f _) ?_
      have : â€–(OrthProj ğ•œ D) xâ€– â‰¤ 1 * â€–xâ€– :=
        le_of_opNorm_le (OrthProj ğ•œ D) (norm_OrthProj_le_one ğ•œ D) x
      simp only [AddSubgroupClass.coe_norm, one_mul] at this
      exact PosMulMono.mul_le_mul_of_nonneg_left (opNorm_nonneg f) this
    Â· repeat rw [norm_def]
      apply csInf_le_csInf
      Â· use â€–fâ€–
        simp only [lowerBounds, AddSubgroupClass.coe_norm, Subtype.forall, Set.mem_setOf_eq,
          and_imp]
        exact fun a ha h => (opNorm_le_iff ha).mpr fun x â†¦ h (â†‘x) x.prop
      Â· use â€–(f.comp (OrthProj ğ•œ D))â€–
        simp only [coe_comp', Function.comp_apply, Set.mem_setOf_eq,
          norm_nonneg, true_and]
        intro x
        rw [â† comp_apply]
        exact le_opNorm (f.comp (OrthProj ğ•œ D)) x
      Â· intro c hc
        simp only [coe_comp', Function.comp_apply, Set.mem_setOf_eq,
          AddSubgroupClass.coe_norm, Subtype.forall] at *
        refine âŸ¨hc.1, fun a ha => ?_âŸ©
        convert hc.2 a
        exact Eq.symm (OrthProj_id ğ•œ D a ha)

/--
A Hahnâ€“Banach style extension theorem for continuous linear maps between ultrametric normed spaces.

Given:
* a nontrivially normed field `ğ•œ`,
* normed `ğ•œ`-vector spaces `E` and `F` equipped with an ultrametric distance
  (`[IsUltrametricDist E]` and `[IsUltrametricDist F]`),
* a submodule `D : Submodule ğ•œ E`,
* a continuous linear map `f : D â†’L[ğ•œ] F`,
* and the assumption that `F` is spherically complete (`[SphericallyCompleteSpace F]`),

this theorem produces a continuous linear map `f' : E â†’L[ğ•œ] F` extending `f` from `D` to all of `E`,
and preserving the operator norm: `â€–f'â€– = â€–fâ€–`.

The extension property is stated pointwise: for any `v : E` with `hv : v âˆˆ D`, we have
`f' v = f âŸ¨v, hvâŸ©`.
-/
theorem hahn_banach' {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
{E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [IsUltrametricDist E]
(D : Submodule ğ•œ E)
{F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [IsUltrametricDist F]
[hf : SphericallyCompleteSpace F] (f : D â†’L[ğ•œ] F) :
âˆƒ f' : E â†’L[ğ•œ] F,
  (âˆ€ v : E, (hv : v âˆˆ D) â†’ f' v = f âŸ¨v, hvâŸ©) âˆ§ â€–f'â€– = â€–fâ€– := by
  if hf : f = 0 then
    use 0
    simp only [zero_apply, hf, implies_true, norm_zero, and_self]
  else
    rcases @exists_extension_opNorm_le ğ•œ _ E _ _ _ D F _ _ _ _ f {0}
      (by simp) (fun _ => â€–fâ€–) (by simp [hf]) (by simp) (by
      simpa using fun a ha => le_opNorm f âŸ¨a, haâŸ©
      ) with âŸ¨f', hf1, hf2âŸ©
    use f'
    simp only [Subtype.forall, Set.mem_singleton_iff, forall_eq, sub_zero] at hf2
    refine âŸ¨fun v hv => hf1 âŸ¨v, hvâŸ©, eq_of_le_of_ge hf2 ?_âŸ©
    repeat rw [norm_def]
    apply csInf_le_csInf
    Â· use â€–fâ€–
      simp only [lowerBounds, AddSubgroupClass.coe_norm, Subtype.forall, Set.mem_setOf_eq,
        and_imp]
      exact fun a ha h => (opNorm_le_iff ha).mpr fun x â†¦ h (â†‘x) x.prop
    Â· use â€–f'â€–
      simp only [Set.mem_setOf_eq, norm_nonneg, true_and]
      exact fun x => le_opNorm f' x
    Â· intro c hc
      simp only [AddSubgroupClass.coe_norm, Subtype.forall, Set.mem_setOf_eq] at *
      refine âŸ¨hc.1, fun a ha => ?_âŸ©
      simpa only [â† (hf1 a ha).symm] using hc.2 a

end SphericallyCompleteSpace
